/////////////////===== Setup Resting Overhaul =====\\\\\\\\\\\\\\\\\

////Add script to force use of supply items
EXTEND_TOP ~baldur.bcs~ ~%MOD_FOLDER%/components/resting/restscript.baf~

////Remove Encounters from All Areas
COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
	READ_LONG 0x0c0 rest_off
	WRITE_LONG rest_off + 0xa4 0 // make inactive, set max creatures to 0, and
	WRITE_LONG rest_off + 0xa8 0 // drop probabilities to zero for good measure

////Add Camping Supplies Item
COPY_EXISTING ~%MOD_FOLDER%/components/resting/ICAMPSP.BAM~ ~override~
COPY_EXISTING ~%MOD_FOLDER%/components/resting/GCAMPSP.BAM~ ~override~
COPY_EXISTING ~%MOD_FOLDER%/components/resting/CAMPSUPP.ITM~ ~override~
	SAY NAME1 @26
	SAY NAME2 @26
	SAY DESC @27
	SAY UNIDENTIFIED_DESC @27
    WRITE_LONG 0x34 camping_cost // set price

////Add to Stores, Taverns and Inns
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
	READ_SHORT  0x08 "type" ELSE 0 //0 = store, 1 = tavern, 2 = inn, 3 = temple, 4 = unused(???), 5 = container	
	PATCH_IF (type <= 2) AND GAME_IS ~bgee bg2ee~ //Stores, Inns and Taverns have unlimited
		BEGIN
			ADD_STORE_ITEM ~campsupp~ LAST #0 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
		END
	PATCH_IF (type == 0) AND GAME_IS ~iwdee~ //Stores have unlimited
		BEGIN
			ADD_STORE_ITEM ~campsupp~ LAST #0 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
		END
BUT_ONLY

////Add Fatigue Potion
COPY_EXISTING ~%MOD_FOLDER%/components/resting/IPOTFAT.BAM~ ~override~
COPY_EXISTING ~%MOD_FOLDER%/components/resting/GPOTFAT.BAM~ ~override~
COPY_EXISTING ~%MOD_FOLDER%/components/resting/CPOTFAT.BAM~ ~override~
COPY_EXISTING ~%MOD_FOLDER%/components/resting/POTFAT.ITM~ ~override~
	//SAY NAME1 @28
	SAY NAME2 @28
	SAY DESC @29
	//SAY UNIDENTIFIED_DESC @29
    WRITE_LONG 0x34 35 // set price to 35 gp
    WRITE_SHORT 0x38 10 // set Stack Size to 10

////Add to Stores
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
	READ_SHORT  0x08 "type" ELSE 0 //0 = store, 1 = tavern, 2 = inn, 3 = temple, 4 = unused(???), 5 = container
	PATCH_IF (type == 3) //Temples have unlimited
		BEGIN
			ADD_STORE_ITEM ~potfat~ LAST #0 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
		END
BUT_ONLY

/*
//Gerth's Equipment Shop - Kuldahar
COPY_EXISTING ~kugerth.sto~ ~override~
	ADD_STORE_ITEM ~campsupp~ LAST #0 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
  
//Pomab's Emporium - Easthaven
COPY_EXISTING ~ehpomab.sto~ ~override~
	ADD_STORE_ITEM ~campsupp~ LAST #0 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
*/

////Increase Prices for Inn
//Inn gives a lot of benefits, since you regain spell slots, so realistically
//it should cost more than temple services, but less than portable camping supplies,
//since it's less convenient
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
	READ_SHORT  0x08 "type" ELSE 0
	PATCH_IF ((type == 2))
		BEGIN
			READ_SHORT  0x60 "peasantPrice" ELSE 0
			READ_SHORT  0x64 "merchantPrice" ELSE 0
			READ_SHORT  0x68 "noblePrice" ELSE 0
			READ_SHORT  0x6c "royalPrice" ELSE 0
			READ_SHORT  0x5c "roomFlags" ELSE 0
			WRITE_LONG 0x60 (peasantPrice * inn_mult) //multiply price
			WRITE_LONG 0x64 (peasantPrice * inn_mult + 2) //multiply price
			WRITE_LONG 0x68 (peasantPrice * inn_mult + 4) //multiply price
			WRITE_LONG 0x6c (peasantPrice * inn_mult + 6) //multiply price
		END
BUT_ONLY

////Fix city flags for various areas (Kuldahar etc) in Icewind Dale
ACTION_IF (GAME_IS ~iwdee~) BEGIN
	LAUNCH_ACTION_FUNCTION add_city_flags STR_VAR area = ~AR1000.ARE~ END
	LAUNCH_ACTION_FUNCTION add_city_flags STR_VAR area = ~AR2100.ARE~ END
	LAUNCH_ACTION_FUNCTION add_city_flags STR_VAR area = ~AR9100.ARE~ END
	LAUNCH_ACTION_FUNCTION add_city_flags STR_VAR area = ~AR9200.ARE~ END
END

//Add a "You cannot rest without camping supplies" message when using the rest button
COPY_EXISTING ~UI.MENU~ ~override~
	SET rest_message=RESOLVE_STR_REF(@102)
	SET rest_enabled=RESOLVE_STR_REF("<REST_ENABLED>")
	REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~e:GetActiveEngine():OnRestButtonClick()~ ~sargeRestButtonClicked()~
	INSERT_FILE 2 ~%MOD_FOLDER%/components/resting/rest.lua~
	REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~@rest_message~ ~%rest_message%~
	REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~@rest_enabled~ ~%rest_enabled%~
BUT_ONLY
